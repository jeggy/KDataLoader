
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

apply from: rootProject.file("gradle/commonVariables.gradle")

bintray {
    user = (project.hasProperty("bintray_user")) ? project.property("bintray_user") : System.getenv('BINTRAY_USER')
    key = (project.hasProperty("bintray_key")) ? project.property("bintray_key") : System.getenv('BINTRAY_API_KEY')
    publications = ['maven']
    //publish = true

    pkg {
        repo = "maven"
        name = project.name
        licenses = ["MIT"]
        labels = ["kotlin", "Kotlin", "DataLoader", "GraphQL"]
        vcsUrl = myVcsUrl
        websiteUrl = myWebsiteUrl
        issueTrackerUrl = "$myVcsUrl/issues"

        version {
            name = project.version
            released = new Date()
        }
    }

}

bintrayUpload.doFirst {
    publications = publishing.publications.collect {
        it.name
    }.findAll {
        it != "kotlinMultiplatform"
    }
}

//The task of bintrayUpload depends on publishToMavenLocal set in the previous section.
// This will create maven artifacts when running bintrayUplaod's task.
bintrayUpload.dependsOn publishToMavenLocal